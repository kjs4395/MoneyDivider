plugins {
    id 'org.springframework.boot' version "$SPRING_BOOT_VERSION"
    id 'nu.studer.jooq' version "$JOOQ_PLUGIN_VERSION"
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.kakaopay'
version = "$PROJECT_VERSION"
sourceCompatibility = "$JAVA_VERSION"
targetCompatibility = "$JAVA_VERSION"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'mysql:mysql-connector-java'
    jooqGenerator 'mysql:mysql-connector-java:5.+'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

jooq {
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'com.mysql.jdbc.Driver'
                    url = "jdbc:mysql://$JOOQ_DB_HOST:$JOOQ_DB_PORT/$JOOQ_SCHEMA"
                    user = "$JOOQ_USER"
                    password = "$JOOQ_PASSWORD"
                }
                generator {
                    database {
                        inputSchema = "$JOOQ_SCHEMA"
                    }
                    generate {
                        javaTimeTypes = true
                        daos = true
                        pojos = true
                    }
                    target {
                        packageName = "$JOOQ_PACKAGE_NAME"
                        directory = "$JOOQ_CODEGEN_DIR"
                    }
                    strategy {
                        name = 'org.jooq.codegen.example.JPrefixGeneratorStrategy'
                    }
                }
            }
        }
    }
}